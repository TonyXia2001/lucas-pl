<b2> ::= <block>*;

<block> ::= <statement> | "{" <statement>* "}";

<statement> ::=

  <variable> "=" (<operand> | <expression> | <string> | <mem_access> | "alloc" "\(" <variable> "\)" | "free" "\(" <variable> "\)") ";" |

  ("print" | "printi" | "read" | "readi") "\(" <variable> "\)" ";" |

  "while" "\(" <expression> "\)" <block> |

  "if" "\(" <expression> "\)" <block> |

  <mem_access> "=" <variable> ";";

<mem_access> ::= "\[" <variable>  "\+" decimal_integer "\]";

<expression> ::=

  <unary_operator> <operand> |

  <operand> <binary_operator> <operand>;

<operand> ::= <variable> | decimal_integer;

<unary_operator> ::= "!";

<binary_operator> ::=

  "\+" | "-"  | "\*" | "\|" |

  "&"  | "==" | "!=" | ">"  |

  ">=" | "<"  | "<=";

<variable> ::= identifier;

<string> ::= escaped_string;
