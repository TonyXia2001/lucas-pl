<b> ::= <block> <b>;

<b> ::= <block>;

<block> ::= <statement>;

<block> ::= "{" <statements> "}";

<statements> ::= <statement> <statements>;

<statements> ::= <statement>;

<statement> ::= <variable> "=" <operand> ";";

<statement> ::= <variable> "=" <expression> ";";

<statement> ::= <variable> "=" <string> ";";

<statement> ::= "print" "\(" <variable> "\)" ";";

<statement> ::= "printi" "\(" <variable> "\)" ";";

<statement> ::= "read" "\(" <variable> "\)" ";";

<statement> ::= "readi" "\(" <variable> "\)" ";";

<statement> ::= "while" "\(" <expression> "\)" <block>;

<statement> ::= "if" "\(" <expression> "\)" <block>;

<expression> ::= <unary_operator> <operand>;

<expression> ::= <operand> <binary_operator> <operand>;

<operand> ::= <variable>;

<operand> ::= decimal_integer;

<unary_operator> ::= "!";

<binary_operator> ::= "\+";

<binary_operator> ::= "-";

<binary_operator> ::= "\*";

<binary_operator> ::= "\|";

<binary_operator> ::= "&";

<binary_operator> ::= "==";

<binary_operator> ::= "!=";

<binary_operator> ::= ">";

<binary_operator> ::= ">=";

<binary_operator> ::= "<";

<binary_operator> ::= "<=";

<variable> ::= identifier;

<string> ::= escaped_string;
